* Org resolve deps

This package will walk all the include-statements of an org-mode file
and temporarily replace them with the content of the files specified
at tangling.
#+begin_src emacs-lisp :noweb yes :tangle yes :comments org
;; <<license>>

(require 'org)

(defgroup org-resolve-deps nil
  "Resolve include-statements before tangling"
  :prefix "org-resolve-deps-"
  :group 'org
  :link '(url-link :tag "Github" "https://github.com/hkjels/org-resolve-deps"))
#+end_src

** Do you prefer ~org-resolve-deps~ over the original tangle functions?

By default, you will have to explicitly use org-resolve-deps tanling
functions to tangle a project, but by setting ~org-resolve-deps-advice~
to ~t~, the original ~org-babel~ tangling functions will be enhanced.
#+begin_src emacs-lisp :tangle yes :comments org
(defcustom org-resolve-deps-advice nil
  "Should include-statements be resolved when using the original
   org-babel tangling functions?"
  :type '(boolean)
  :group 'org-resolve-deps)
#+end_src

** Resolve dependencies & tangle

We need to check the ~:tangle~ argument and make it adhere to how we
resolve dependencies.
#+begin_src emacs-lisp :tangle yes :comments org
(defun org-resolve-deps-header-tangle (str root file)
  "Ensure that the tangle argument uses the path specified
If `:tangle` is set to `yes` it will use the current file as base.
`org-resolve-deps` introduces a new `:tangle` option `root`.
Specifying `root` will tangle using the root-file as base. Using a
file-path as argument, works as you'd expect; with relative path being
relative to the file it's in."
  (replace-regexp-in-string
   ":tangle\s*\\([^\s\n:]+\\)"
   (lambda (tangle)
     (or (cond ((string= "yes" tangle) (file-name-sans-extension file))
               ((string= "root" tangle) root)
               ((string= "no" tangle) "no")
               ((> (length tangle) 0)
                (if (file-name-absolute-p tangle)
                    tangle
                  (expand-file-name (file-name-sans-extension file) (file-name-directory root)))))
         "no"))
   str))
#+end_src

This is where the bulk of the heavy lifting happens. We walk the
dependencies ("include statements") and replace them with the content
of the files they point to. We also keep track of what files have been
included to not end up with cyclic dependencies.
#+begin_src emacs-lisp :tangle yes :comments org
(defun org-resolve-deps-of-file (root &optional file start files-included)
  "Recursively replace include-statements with the targeted content"
  (let* ((file (or file root))
         (basedir (file-name-directory file))
         (pattern "^#\\+include:\s*\"\\([^\"]+\\)\"[^\n]*$")
         (start (or start (point-min)))
         (files-included (or files-included '())))
    (if (member file files-included)
        (user-error "org-resolve-deps: Found a cyclic dependency `%s`" file)
      (with-temp-buffer
        (insert-file-contents file)
        (goto-char start)
        (while (re-search-forward pattern nil 'noerror)
          (let* ((include-file (match-string 1))
                 (path (expand-file-name include-file basedir))
                 (new-start (match-end 0))
                 (files-included (cons file files-included))
                 (tree (org-resolve-deps-of-file root path new-start files-included)))
            (replace-match (org-resolve-deps-header-tangle tree root path))))
        (buffer-string)))))
#+end_src

You call ~org-resolve-deps-tangle-file~ or ~org-resolve-deps-tangle~ just
as you would call the original ~org-babel-tangle~ functions. If you
supply any arguments, the call is just relayed to the original
functions as ~org-resolve-deps~ only work on a per project level.
#+begin_src emacs-lisp :tangle yes :comments org
(defun org-resolve-deps-tangle-file (file &optional target-file lang)
  "Tangle the specified file and all of it's includes"
  (interactive "P")
  (if (and (equal nil target-file)
           (equal nil lang))
      (if (file-exists-p file)
          (let* ((buf (generate-new-buffer "*org-resolve-deps*"))
                 (basedir (file-name-directory file))
                 (tmp-file (expand-file-name ".org-resolve-deps.org" basedir)))
            (with-current-buffer buf
              (insert (org-resolve-deps-of-file file))
              (write-file tmp-file))
            (org-babel-tangle-file tmp-file))
        (user-error "`org-resolve-deps`: Only files saved to disk can be tangled"))
    (progn (message "`org-resolve-deps`: Falling back to using original `org-babel-tangle-file` function")
           (org-babel-tangle-file file target-file lang))))

(defun org-resolve-deps-tangle (&optional arg target-file lang)
  "Tangle the current buffer and all of it's includes"
  (interactive "P")
  (if (equal nil arg)
      (org-resolve-deps-tangle-file (buffer-file-name) target-file lang)
      (progn (message "`org-resolve-deps`: Falling back to using original `org-babel-tangle` function")
             (org-babel-tangle arg target-file lang))))
#+end_src

Here we choose which functions to apply based on the value of ~org-resolve-deps-advice~.
#+begin_src emacs-lisp :tangle yes :comments org
(define-minor-mode org-resolve-deps-mode
  "Resolve includes before tangling"
  nil
  :lighter ""
  :global t
  :require 'org-resolve-deps
  (when org-resolve-deps-mode
      (advice-add 'org-babel-tangle :around
                  (lambda (cmd &rest args)
                    (if org-resolve-deps-advice
                        (apply #'org-resolve-deps-tangle args)
                      (apply cmd args))))
      (advice-add 'org-babel-tangle-file :around
                  (lambda (cmd &rest args)
                    (if org-resolve-deps-advice
                        (apply #'org-resolve-deps-tangle-file args)
                      (apply cmd args))))))
#+end_src

#+begin_src emacs-lisp :tangle yes :comments org
(provide 'org-resolve-deps)
#+end_src

* License

#+name: year
#+begin_src shell :exports none
date +'%Y'
#+end_src

#+name: license
#+begin_src text :tangle LICENSE.txt :noweb yes
    Copyright (C) <<year()>>  Henrik Kjerringv√•g

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
#+end_src
